plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "de.undercouch.download" version "5.5.0"
}

group = 'com.ampznetwork.worldmod'

sourceSets {
    main.java.srcDirs = files("${parent.projectDir}/src/$project.name/main/java")
    main.resources.srcDirs = files("${parent.projectDir}/src/$project.name/main/resources")
    test.java.srcDirs = files("${parent.projectDir}/src/$project.name/test/java")
    test.resources.srcDirs = files("${parent.projectDir}/src/$project.name/test/java")
}

tasks.register('dlSpigotServerJar', Download).configure {
    src 'https://download.getbukkit.org/spigot/spigot-1.20.1.jar'
    dest "$projectDir/run/spigot.jar"
}

tasks.register('dlPaperServerJar', Download).configure {
    src 'https://api.papermc.io/v2/projects/paper/versions/1.20.1/builds/196/downloads/paper-1.20.1-196.jar'
    dest "$projectDir/run/paper.jar"
}

tasks.register('dlArclightServerJar', Download).configure {
    src 'https://github.com/IzzelAliz/Arclight/releases/download/Trials%2F1.0.5/arclight-forge-1.20.1-1.0.5.jar'
    dest "$projectDir/run/arclight.jar"
}

shadowJar {
    // Specify which projects' classes to include
    from(project(":${prefix}:api").sourceSets.main.output)
    from(project(":${prefix}:core").sourceSets.main.output)

    // Exclude dependencies by ensuring the JAR contains only the classes
    configurations = [project.configurations.shadow]
}

tasks.register('testPlugin', Copy) {
    dependsOn shadowJar, dlArclightServerJar, dlPaperServerJar, dlSpigotServerJar
    from shadowJar.outputs.files.singleFile
    into "${projectDir}/run/plugins"
    rename { n -> "WorldMod-spigot.jar" }
}

task simplifyArtifact(type: Copy) {
    dependsOn test, shadowJar
    from shadowJar.outputs.files.singleFile
    into "$rootDir/build/libs/"
    rename { n -> "WorldMod-spigot.jar" }
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    // spigot
    implementation 'org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bungeecord:4.3.3'

    // worldmod api
    implementation project("$prefix:core")

    // rabbit
    runtimeOnly 'com.rabbitmq:amqp-client:5.+'

    // logger
    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'

    // placeholderapi
    compileOnly 'me.clip:placeholderapi:2.+'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
