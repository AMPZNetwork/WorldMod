plugins {
    id 'application'
    id 'org.springframework.boot' version '3.0.+'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'org.comroid.mcsd'
sourceCompatibility = '17'
targetCompatibility = '17'
mainClassName = 'org.comroid.mcsd.agent.Program'

bootRun {
    if (System.getenv().containsKey('DEBUGGER')) {
        System.out.println("Using remote Debugger")
        jvmArgs(['-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005'])
    }
}

sourceSets {
    main.java.srcDirs       = files(parent.projectDir.toString() + "/src/agent/main/java")
    main.resources.srcDirs  = files(parent.projectDir.toString() + "/src/agent/main/resources")
    test.java.srcDirs       = files(parent.projectDir.toString() + "/src/agent/test/java")
    test.resources.srcDirs  = files(parent.projectDir.toString() + "/src/agent/test/java")
}

repositories {
    maven { url 'https://maven.comroid.org' }
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation project(':core')

    if (findProject(':japi') != null)
        implementation project(':japi')
    else implementation 'org.comroid:api:+'

    // json
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.+'

    // db
    implementation 'com.mysql:mysql-connector-j'

    // data
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.+'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework:spring-messaging:6.0.0'

    // security
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // ssh
    implementation 'org.apache.sshd:sshd-core:2.+'
    implementation 'org.apache.sshd:sshd-sftp:2.+'

    // minecraft
    implementation 'io.github.fragland:MineStat:3.+'
    //implementation 'com.github.jaqobb:namemc-api:master-SNAPSHOT'
    implementation 'io.github.MrGraversen:minecraft-rcon:0.+'

    // webjars
    implementation 'org.webjars:webjars-locator-core'
    implementation 'org.webjars:sockjs-client:1.5.1'
    implementation 'org.webjars:stomp-websocket:2.3.4'
    implementation 'org.webjars:jquery:3.7.0'

    // utility
    developmentOnly 'org.springframework.boot:spring-boot-starter-actuator'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test'
}
